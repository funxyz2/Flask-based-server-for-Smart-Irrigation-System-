#include <WiFi.h>
#include <HTTPClient.h>
#include "DHT.h"
#include "secrets.h"

#define DHTPIN 32
#define DHTTYPE DHT22
#define WATER_LEVEL_THRESHOLD 20 // Tr√™n 20% th√¨ b·∫≠t c·∫£m bi·∫øn m·ª±c n∆∞·ªõc (1/0)
DHT dht(DHTPIN, DHTTYPE);

// Blynk
#define BLYNK_TEMPLATE_ID "TMPL6QX3iHsMo"
#define BLYNK_TEMPLATE_NAME "SMART IRRIGATION ESP32"
#define BLYNK_PRINT Serial

#include <BlynkSimpleEsp32.h>

// Pin 
#define ANALOG_WATER_LEVEL_PIN 34
#define ANALOG_SOIL_MOISTURE_PIN 33
#define DIGITAL_SOIL_MOISTURE_PIN 2
#define PUMP_PIN 23

// Setup
char ssid[] = WIFI_SSID; 
char pass[] = WIFI_PASSWORD; 
char auth[] = BLYNK_AUTH_TOKEN;

const char* serverName = FLASK_SERVER_URL; 

bool isManualMode = false; // Ch·∫ø ƒë·ªô th·ªß c√¥ng t·ª´ Blynk App
bool pumpState = false;
unsigned long pumpStartTime = 0;
unsigned long pumpDuration = 0;
bool isPumpRunning = false;
float mlWaterGlobal = 0; // l∆∞u tr·∫°ng th√°i ƒë√£ t∆∞·ªõi
String lastPredictionStr = ""; // L∆∞u k·∫øt qu·∫£ d·ª± ƒëo√°n

BlynkTimer timer;
float last_watered_hour = 0;

struct SensorData {
  int soil_moisture;
  bool water_level;
  float temperature;
  float humidity;
  int digital_soil_value;
};

SensorData data = {};

void readSensors() {
  int maxRetries = 5;
  bool success = false;

  for (int i = 0; i < maxRetries; i++) {
    data.soil_moisture = map(analogRead(ANALOG_SOIL_MOISTURE_PIN), 2500, 4095, 100, 0);
    data.soil_moisture = constrain(data.soil_moisture, 0, 100);
      
    /* FIX
    ·ªû ƒë√¢y ƒë√°ng l·∫Ω d√πng int cho water_level (0% -> 100%)
    Tuy nhi√™n v√¨ dataset train model bu·ªôc c·ªôt n√†y l√† bool (1/0)
    V√¨ v·∫≠y, ta x√≥a ƒëi logic ·∫•y v√† thay b·∫±ng 1/0
      
    //data.water_level = map(analogRead(ANALOG_WATER_LEVEL_PIN), 0, 4095, 0, 100);
    //data.water_level = constrain(data.water_level, 0, 1);
    */  
    int water_level_percent = map(analogRead(ANALOG_WATER_LEVEL_PIN), 0, 4095, 0, 100);
    data.water_level = (water_level_percent >= WATER_LEVEL_THRESHOLD) ? 1 : 0;
    data.temperature = dht.readTemperature();
    data.humidity = dht.readHumidity();

    if (!isnan(data.temperature) && !isnan(data.humidity)) {
      success = true;

      /*
      X·ª≠ l√Ω d·ªØ li·ªáu t·ª´ c√°c c·∫£m bi·∫øn v√† in ra Serial, ƒë·ªìng th·ªùi g√°n c√°c gi√° tr·ªã t∆∞∆°ng ·ª©ng c√°c bi·∫øn trong Blynk App
      */
      Serial.print("Soil Moisture: "); Serial.println(data.soil_moisture);
      Serial.print("Water level: "); Serial.println(water_level_percent);
      Serial.print("Temperature: "); Serial.println(data.temperature);
      Serial.print("Humidity: "); Serial.println(data.humidity);
      Serial.println("--------------------------------------------");

      Blynk.virtualWrite(V2, data.soil_moisture);
      Blynk.virtualWrite(V3, water_level_percent);
      Blynk.virtualWrite(V0, data.temperature);
      Blynk.virtualWrite(V1, data.humidity);

      break;
    }

    Serial.println("‚ö†Ô∏è L·ªói ƒë·ªçc c·∫£m bi·∫øn! ƒêang th·ª≠ l·∫°i...");
    delay(1000); 
  }

  if (!success) {
    Serial.println("‚ùå Kh√¥ng ƒë·ªçc ƒë∆∞·ª£c c·∫£m bi·∫øn sau nhi·ªÅu l·∫ßn th·ª≠. T·∫°m d·ª´ng ch∆∞∆°ng tr√¨nh.");
    // G√°n gi√° tr·ªã ƒë·∫∑c bi·ªát ƒë·ªÉ h√†m sendToServer() d·ª´ng l·∫°i
    data.temperature = NAN;
    data.humidity = NAN;
  }
}

void setup() {
  Serial.begin(9600);
  Serial.println("B·∫°n v·ª´a b·∫≠t H·ªá th·ªëng T∆∞·ªõi ti√™u th√¥ng minh, ƒë·ªÉ h·ªá th·ªëng ho·∫°t ƒë·ªông ƒë√∫ng, b·∫°n c·∫ßn ph·∫£i t∆∞·ªõi cho c√¢y c·ªßa b·∫°n ƒë·ªß n∆∞·ªõc");
  
  // L∆∞u th·ªùi ƒëi·ªÉm ƒë√£ t∆∞·ªõi
  last_watered_hour = millis() / 3600000.0;

  // pinMode
  pinMode (DIGITAL_SOIL_MOISTURE_PIN, INPUT);
  pinMode (ANALOG_WATER_LEVEL_PIN, INPUT);
  pinMode (ANALOG_SOIL_MOISTURE_PIN, INPUT);
  pinMode(PUMP_PIN, OUTPUT);
  digitalWrite(PUMP_PIN, LOW);

  Blynk.begin(auth, ssid, pass);
  dht.begin(); 
  delay(2000); // ƒê·ª£i c·∫£m bi·∫øn ·ªïn ƒë·ªãnh

  timer.setInterval(1000L, readSensors);             // C·∫≠p nh·∫≠t UI Blynk App m·ªói 1 gi√¢y
  timer.setInterval(7L * 60L * 60L * 1000L, sendToServer);  // G·ª≠i Flask server m·ªói 7 ti·∫øng
}

void loop() {
  Blynk.run();
  timer.run();

  // T·∫Øt m√°y b∆°m sau khi ƒë·ªß th·ªùi gian
  if (isPumpRunning && millis() - pumpStartTime >= pumpDuration) {
    digitalWrite(PUMP_PIN, LOW);
    isPumpRunning = false;
    Serial.println("Pump finished automatically based on ML result.");

    if (!isManualMode) {
    pumpState = false;
    Blynk.virtualWrite(V4, 0);
    }
  }
}

// X·ª≠ l√Ω delay duration cho m√°y b∆°m
void runPumpForML(float ml) {
  pumpDuration = ml / 10.0 * 1000; // ml / t·ªëc ƒë·ªô (10ml/s) * 1000ms
  pumpStartTime = millis();
  digitalWrite(PUMP_PIN, HIGH);
  isPumpRunning = true;
}

void sendToServer() {
  if (WiFi.status() == WL_CONNECTED) {
    HTTPClient http;
    readSensors();

    // T√≠nh s·ªë gi·ªù ƒë√£ tr√¥i qua k·ªÉ t·ª´ l·∫ßn t∆∞·ªõi cu·ªëi
    float current_hour = millis() / 3600000.0;
    int hours_since_last_watering = current_hour - last_watered_hour;
    Serial.print("Hours since last watering: "); Serial.println(hours_since_last_watering);

    String jsonData = "{\"temperature\":" + String(data.temperature) +
                      ",\"soil_moisture\":" + String(data.soil_moisture) +
                      ",\"water_level\":" + String(data.water_level) +
                      ",\"humidity_air\":" + String(data.humidity) +
                      ",\"last_watered_hour\":" + String(hours_since_last_watering) + "}";

    http.setTimeout(3000); // TImeout 3 gi√¢y
    http.begin(serverName);
    http.addHeader("Content-Type", "application/json");

    /*
    Nh·∫≠n Response v√† x·ª≠ l√Ω
    */
    int httpResponseCode = http.POST(jsonData);
    delay(200);

    if (httpResponseCode == 200) {
      String response = http.getString();
      Serial.println("Response: " + response);
      lastPredictionStr = response;  // Ghi chu·ªói response l·∫°i cho V7
      float mlWater = response.toFloat();
      Serial.print("I will pump this much water: "); Serial.print(mlWater); Serial.println("ml");

      // N·∫øu Model y√™u c·∫ßu t∆∞·ªõi
      if (mlWater > 0) {
        runPumpForML(mlWater);
        // C·∫≠p nh·∫≠t th·ªùi ƒëi·ªÉm t∆∞·ªõi g·∫ßn nh·∫•t
        last_watered_hour = millis() / 3600000.0;
        mlWaterGlobal = mlWater;
      }
    } else {
      Serial.print("POST failed. Error Code: "); Serial.println(httpResponseCode);
    }

    http.end();
  } else {
    Serial.println("WiFi not connected");
  }
}

// Callback khi switch V4 thay ƒë·ªïi tr·∫°ng th√°i
BLYNK_WRITE(V4) {
  // Ch·∫∑n user chuy·ªÉn sang manual mode khi ƒëang h·ªá th·ªëng ƒëang t∆∞·ªõi
  if (isPumpRunning) {
    Serial.println("‚ö†Ô∏è Pump is running from ML. Manual control is disabled.");
    Blynk.virtualWrite(V4, pumpState ? 1 : 0); // Reset l·∫°i tr·∫°ng th√°i tr√™n app
    return;
  }

  if (isManualMode)
  {
    int value = param.asInt();
    digitalWrite(PUMP_PIN, value == 1 ? HIGH : LOW);
    pumpState = (value == 1);
    Blynk.virtualWrite(V4, pumpState ? 1 : 0);
    Serial.println(pumpState ? "Pump turned ON by user from BlynkApp" : "Pump turned OFF by user from BlynkApp");
  } else {
    Serial.println("PUMP (DE)ACTIVATION IS NOT PERMITTED...");
    // Th·ª±c hi·ªán ghi l·∫°i tr·∫°ng th√°i cho switch V4 (ƒë·ªìng b·ªô ho√° tr·∫°ng th√°i)
    Blynk.virtualWrite(V4, pumpState ? 1 : 0);
  }
}

// Callback khi switch V4 thay ƒë·ªïi tr·∫°ng th√°i
BLYNK_WRITE(V5) {
  int mode = param.asInt();
  isManualMode = (mode == 1);
  Serial.println(isManualMode ? "INITIATING MANUAL MODE..." : "TURNING OFF MANUAL MODE...");
  
  // ƒê·∫£m b·∫£o m√°y b∆°m lu√¥n t·∫Øt khi kh√¥ng d√πng
  if (!isManualMode && pumpState) {
    // N·∫øu ƒëang t∆∞·ªõi tay, v√† v·ª´a chuy·ªÉn sang auto mode th√¨ t·∫Øt b∆°m
    digitalWrite(PUMP_PIN, LOW);
    pumpState = false;
    Blynk.virtualWrite(V4, 0);
    Serial.println("üõë Pump OFF: Switched to Auto mode");
  }
}

// G·ªçi API th·ªß c√¥ng
BLYNK_WRITE(V6) {
  int value = param.asInt();
  if (value == 1) {
    Serial.println("üîÅ Manual Predict Triggered from Blynk App!");
    sendToServer();  // G·ªçi h√†m g·ª≠i d·ªØ li·ªáu ƒë·∫øn server Flask
    Blynk.virtualWrite(V6, 0); // Reset l·∫°i n√∫t v·ªÅ OFF sau khi nh·∫•n

    // G·ª≠i k·∫øt qu·∫£ d·ª± ƒëo√°n v·ªÅ Blynk
    if (lastPredictionStr == "") {
      Serial.println("‚ö†Ô∏è No prediction result available.");
      return;
    }
    String result = lastPredictionStr + " ml";
    Serial.print("Sending last prediction string to Blynk: ");
    Serial.println(result);

    Blynk.virtualWrite(V7, result);
  }
}

// Button Blynk reset ph·∫ßn m·ªÅm trong ESP32
BLYNK_WRITE(V8) {
  int value = param.asInt();
  if (value == 1) {
    Serial.println("üîÅ Ng∆∞·ªùi d√πng y√™u c·∫ßu kh·ªüi ƒë·ªông l·∫°i thi·∫øt b·ªã...");
    ESP.restart();
  }
}